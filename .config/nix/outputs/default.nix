{
  self,
  nixpkgs,
  pre-commit-hooks,
  darwin-custom-icons,
  zjstatus,
  ...
} @ inputs: let
  inherit (inputs.nixpkgs) lib;
  mylib = import ../lib {inherit lib;};
  myvars = import ../vars {inherit lib;};

  # Add my custom lib, vars, nixpkgs instance, and all the inputs to specialArgs,
  # so that I can use them in all my nixos/home-manager/darwin modules.
  genSpecialArgs = system:
    inputs
    // {
      inherit mylib myvars;

      pkgs = import inputs.nixpkgs {
        inherit system;
        # To use 1Password, we need to allow the installation of non-free software
        config.allowUnfree = true;
        overlays = [
          (prev: {
            zjstatus = zjstatus.packages.${prev.system}.default;
            sbarLua = prev.callPackage "${self}/packages/sketchybar/helpers/sbar.nix" {};
            sketchybarConfigLua = prev.callPackage "${self}/packages/sketchybar" {};
            sbarMenus = prev.callPackage "${self}/packages/sketchybar/helpers/menus" {};
            sbarEvents = prev.callPackage "${self}/packages/sketchybar/helpers/event_providers" {};
          })
        ];
      };
      # use unstable branch for some packages to get the latest updates
      pkgs-unstable = import inputs.nixpkgs-unstable {
        inherit system; # refer the `system` parameter form outer scope recursively
        # To use chrome, we need to allow the installation of non-free software
        config.allowUnfree = true;
      };
      pkgs-stable = import inputs.nixpkgs-stable {
        inherit system;
        # To use chrome, we need to allow the installation of non-free software
        config.allowUnfree = true;
      };
    };

  # This is the args for all the haumea modules in this folder.
  args = {inherit inputs lib mylib myvars darwin-custom-icons genSpecialArgs;};

  nixosSystems = {};
  darwinSystems = {
    aarch64-darwin = import ./aarch64-darwin (args // {system = "aarch64-darwin";});
  };
  allSystems = nixosSystems // darwinSystems;
  allSystemNames = builtins.attrNames allSystems;
  nixosSystemValues = builtins.attrValues nixosSystems;
  darwinSystemValues = builtins.attrValues darwinSystems;

  # Helper function to generate a set of attributes for each system
  forAllSystems = func: (nixpkgs.lib.genAttrs allSystemNames func);
in {
  # Add attribute sets into outputs, for debugging
  debugAttrs = {inherit nixosSystems darwinSystems allSystems allSystemNames;};

  # NixOS Hosts
  nixosConfigurations =
    lib.attrsets.mergeAttrsList (map (it: it.nixosConfigurations or {}) nixosSystemValues);

  # macOS Hosts
  darwinConfigurations =
    lib.attrsets.mergeAttrsList (map (it: it.darwinConfigurations or {}) darwinSystemValues);

  # Packages
  packages = forAllSystems (
    system: allSystems.${system}.packages or {}
  );

  checks = forAllSystems (
    system: {
      pre-commit-check = pre-commit-hooks.lib.${system}.run {
        src = mylib.relativeToRoot ".";
        hooks = {
          alejandra.enable = true; # formatter
          # typos.enable = true; # Source code spell checker
          # prettier.enable = true;
          deadnix.enable = true; # detect unused variable bindings in `*.nix`
          statix.enable = true; # lints and suggestions for Nix code(auto suggestions)
          # If you want to enable settings for a hook, use hooks.<hook>.settings
          # hooks.typos.settings = {
          #   write = true;
          #   configPath = "./.typos.toml";
          # };
          # hooks.prettier.settings = {
          #   write = true;
          #   configPath = "./.prettierrc.yaml";
          # };
        };
      };
    }
  );

  # Format the nix code in this flake
  formatter = forAllSystems (
    # alejandra is a nix formatter with a beautiful output
    system: nixpkgs.legacyPackages.${system}.alejandra
  );
}
